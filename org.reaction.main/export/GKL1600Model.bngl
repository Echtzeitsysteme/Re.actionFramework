begin model

begin parameters
	initAmount	1600.0
end parameters

begin molecule types
	K(a)
	P(a)
	T(x~u~p,y~u~p)
end molecule types

begin species
	P(a)	initAmount
	T(x~u,y~u)	initAmount
	K(a)	initAmount
end species

begin reaction rules
	KT_x:	T(x) + K(a) <-> T(x!1).K(a!1)		1.0, 10.0
	Tp_x:	T(x~u!1).K(a!1) -> T(x~p!1).K(a!1)		1.0
	KT_y:	T(y) + K(a) <-> T(y!1).K(a!1)		1.0, 10.0
	Tp_y:	T(y~u!1).K(a!1) -> T(y~p!1).K(a!1)		1.0
	PT_x:	P(a) + T(x) <-> P(a!1).T(x!1)		1.0, 10.0
	Tu_x:	P(a!1).T(x~p!1) -> P(a!1).T(x~u!1)		1.0
	PT_y:	P(a) + T(y) <-> P(a!1).T(y!1)		1.0, 10.0
	Tu_y:	P(a!1).T(y~p!1) -> P(a!1).T(y~u!1)		1.0
end reaction rules

begin observables
	Molecules T_pp	T(x~p!?,y~p!?)
	Molecules T_pp_unbound	T(x~p,y~p)
end observables

end model

#----------------Simulation Details below----------------#

generate_network({overwrite=>1})

simulate({method=>"ssa", t_end=>100, n_steps=>100})